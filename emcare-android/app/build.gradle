plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 33
    buildFeatures {
        buildConfig = true
    }
//    kapt {
//        enabled = false
//    }
    defaultConfig {
        applicationId "com.argusoft.who.emcare"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 6
        versionName "1.2.5"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL_TERMS", "\"http://3.111.94.246:8080/\""
            buildConfigField "String", "BASE_URL", "\"http://3.111.94.246:8080/api/\""
            buildConfigField "String", "FHIR_BASE_URL", "\"http://3.111.94.246:8080/fhir/\""
            buildConfigField "String", "KEYCLOAK_BASE_URL", "\"http://3.111.94.246:8080/auth/realms/emcare/protocol/openid-connect/\""
            shrinkResources false
            minifyEnabled false
//            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            resValue "string", "app_version", "${defaultConfig.versionName}.debug"
            resValue "string", "app_name", "EmCare debug"
            manifestPlaceholders = [
                    appIcon: "@drawable/emcare_logo_dev",
                    appIconRound: "@drawable/emcare_logo_dev"
            ]
            applicationIdSuffix ".debug"
        }
        dev {
            buildConfigField "String", "BASE_URL_TERMS", "\"http://3.111.94.246:8080/\""
            buildConfigField "String", "BASE_URL", "\"http://3.111.94.246:8080/api/\""
            buildConfigField "String", "FHIR_BASE_URL", "\"http://3.111.94.246:8080/fhir/\""
            buildConfigField "String", "KEYCLOAK_BASE_URL", "\"http://3.111.94.246:8080/auth/realms/emcare/protocol/openid-connect/\""
            shrinkResources false
            minifyEnabled false
//            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            resValue "string", "app_version", "${defaultConfig.versionName}.dev"
            resValue "string", "app_name", "EmCare dev"
            manifestPlaceholders = [
                    appIcon: "@drawable/emcare_logo_dev",
                    appIconRound: "@drawable/emcare_logo_dev"
            ]
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "BASE_URL_TERMS", "\"https://emcare.argusoft.com/\""
            buildConfigField "String", "BASE_URL", "\"https://emcare.argusoft.com/api/\""
            buildConfigField "String", "FHIR_BASE_URL", "\"https://emcare.argusoft.com/fhir/\""
            buildConfigField "String", "KEYCLOAK_BASE_URL", "\"https://emcare.argusoft.com/auth/realms/emcare/protocol/openid-connect/\""
            minifyEnabled false
            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "${defaultConfig.versionName}.release"
            resValue "string", "app_name", "EmCare"
            manifestPlaceholders = [
                    appIcon: "@drawable/emcare_logo_2",
                    appIconRound: "@drawable/emcare_logo_2"
            ]
        }

        staging {
            buildConfigField "String", "BASE_URL_TERMS", "\"http://65.0.190.33:8080/\""
            buildConfigField "String", "BASE_URL", "\"http://65.0.190.33:8080/api/\""
            buildConfigField "String", "FHIR_BASE_URL", "\"http://65.0.190.33:8080/fhir/\""
            buildConfigField "String", "KEYCLOAK_BASE_URL", "\"http://65.0.190.33:8180/auth/realms/emcare/protocol/openid-connect/\""
            minifyEnabled false
            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "${defaultConfig.versionName}.staging"
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "EmCare Staging"
            manifestPlaceholders = [
                    appIcon: "@drawable/emcare_logo_staging",
                    appIconRound: "@drawable/emcare_logo_staging"
            ]
            signingConfig signingConfigs.debug
        }
    }


    bundle {
        language {
            enableSplit = false
        }
    }

    lintOptions {
        // In general we want to check for problems. Any time, all the time.
        checkReleaseBuilds true
        // And fail the build if we have errors
        abortOnError true

        // Level depends on the type AccountModel the job
        /// Fail the build if buildGreenhouseRelease target is called and we have missing translations!
        warning("MissingTranslation")
        // Error level things
        /// Hardcoded text should not exist or should have case by case exceptions
        error "HardcodedText"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        // Enable Coroutines and Flow APIs
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
    }

    kotlin { jvmToolchain(11) }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        pickFirst("META-INF/ASL-2.0.txt")
        pickFirst("META-INF/LGPL-3.0.txt")
        exclude "license.html"
        exclude "readme.html"
        exclude "META-INF/sun-jaxb.episode"
        exclude "META-INF/LICENSE.md"
        exclude "META-INF/NOTICE.md"
    }

    configurations.all {
        resolutionStrategy {
            force 'com.github.ben-manes.caffeine:caffeine:2.9.1'
//            force 'ca.uhn.hapi.fhir:org.hl7.fhir.r4:5.6.36'
        }

        exclude module: 'commons-logging'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Support library dependency
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Retrofit dependency
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // Moshi converter dependency
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation 'androidx.paging:paging-common-ktx:3.2.0'
    implementation "androidx.paging:paging-runtime-ktx:3.2.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.14.0"
    implementation("com.squareup.moshi:moshi:1.14.0")

    // ViewModel & LiveData dependency
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Room dependency
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Timber dependency
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // For Managing Dimension based on different device
    implementation "com.intuit.sdp:sdp-android:$dimensionVersion"
    implementation "com.intuit.ssp:ssp-android:$dimensionVersion"

    // Glide: For Display url in ImageView
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // For developers using AndroidX in their applications
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Location dependency
    implementation 'com.google.android.gms:play-services-location:19.0.1'

    // Encryption dependency
    implementation 'com.google.crypto.tink:tink-android:1.6.0'

    //Hilt dependency
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation("androidx.hilt:hilt-work:1.0.0")
    // When using Kotlin.
    kapt("androidx.hilt:hilt-compiler:1.0.0")

    //Fragment ktx
    implementation 'androidx.fragment:fragment-ktx:1.6.0'
    implementation 'androidx.activity:activity-ktx:1.7.2'

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // InApp Update & In app Review
    implementation 'com.google.android.play:core-ktx:1.8.1'

    //Work Manager
    implementation "androidx.work:work-runtime-ktx:2.8.1"

    //Fhir
    //Local Build Link: https://drive.google.com/file/d/1Btbbn04MUKdl1N3wKd2lRkJ2S1u6644r/view?usp=share_link
    implementation "com.google.android.fhir:engine:0.1.0-beta05"
    implementation "com.google.android.fhir:data-capture:1.0.0"
    implementation "com.google.android.fhir:workflow:0.1.0-alpha04"
    implementation "com.google.android.fhir:knowledge:0.1.0-alpha03"
    //EventBus
    implementation("org.greenrobot:eventbus:3.3.1")

    //Gson Converter
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Firebase crash analytics
    implementation platform('com.google.firebase:firebase-bom:32.1.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

}